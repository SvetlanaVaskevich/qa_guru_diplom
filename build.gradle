plugins{
    id 'java-library'
    id "io.qameta.allure" version "2.9.6"
}

repositories{
    mavenCentral()
}

def allureVersion = "2.17.2",
    selenideVersion = "6.2.1",
    restAssured = "4.4.0" ,
    junitVersion = "5.8.2" ,
    assertjVersion = "3.22.0",
    ownerVersion = "1.0.12",
    appiumVersion = "8.0.0",
    slf4jVersion = "1.7.32",
    commonVersion = "2.11.0"

allure {
    adapter {
        aspectjWeaver.set(true)
        frameworks {
            junit5 {
                adapterVersion.set(allureVersion)
            }
        }
    }
    report {
        version.set(allureVersion)
    }
}

dependencies {
    testImplementation(
            "commons-io:commons-io:$commonVersion",
            "io.rest-assured:rest-assured:$restAssured",
            "com.codeborne:selenide:$selenideVersion",
            "io.rest-assured:json-schema-validator:$restAssured",
            "io.qameta.allure:allure-rest-assured:$allureVersion",
            "io.qameta.allure:allure-selenide:$allureVersion",
            "org.assertj:assertj-core:$assertjVersion",
            "org.junit.jupiter:junit-jupiter:$junitVersion",
            "org.aeonbits.owner:owner:$ownerVersion",
            "io.appium:java-client:$appiumVersion",
    )
    testRuntimeOnly(
            "org.slf4j:slf4j-simple:$slf4jVersion",
            "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
    )
}

tasks.withType(JavaCompile){
    options.encoding = 'UTF-8'
}

tasks.withType(Test) {
    systemProperties(System.getProperties())

    if (System.getProperty("threads") != null) {
        systemProperties += [
                'junit.jupiter.execution.parallel.enabled'                 : true,
                'junit.jupiter.execution.parallel.mode.default'            : 'concurrent',
                'junit.jupiter.execution.parallel.mode.classes.default'    : 'concurrent',
                'junit.jupiter.execution.parallel.config.strategy'         : 'fixed',
                'junit.jupiter.execution.parallel.config.fixed.parallelism': System.getProperty("threads").toInteger()
        ]
    }

    testLogging {
        lifecycle {
//            events "started", "failed"
            events "started", "skipped", "failed", "standard_error", "standard_out"
            exceptionFormat "short"
        }
    }
}

test {
    useJUnitPlatform()
}

task ui_tests(type: Test) {
    useJUnitPlatform {
        includeTags "ui"
    }
}

task mobile_tests(type: Test) {
    useJUnitPlatform {
        includeTags "mobile"
    }
}
